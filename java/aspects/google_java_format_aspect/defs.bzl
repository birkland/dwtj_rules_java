'''Defines the `google_java_format_aspect` and some helper definitions for it.
'''

load("@dwtj_rules_java//java:providers/JavaCompilationInfo.bzl", "JavaCompilationInfo")

def _extract_google_java_format_deploy_jar(aspect_ctx):
    return aspect_ctx.toolchains['@dwtj_rules_java//java/toolchains/google_java_format_toolchain:toolchain_type'] \
            .google_java_format_toolchain_info \
            .google_java_format_deploy_jar

def _extract_java_executable(aspect_ctx):
    return aspect_ctx.toolchains['@dwtj_rules_java//java/toolchains/java_runtime_toolchain:toolchain_type'] \
            .java_runtime_toolchain_info \
            .java_executable


def _to_path(file):
    '''Used as a map function.'''
    return file.path

GoogleJavaFormatAspectInfo = provider(
    fields = {
        'output_file': "The output file generated by `google-java-format`. This will be `None` if the target doesn't include any Java sources to be formatted.",
    }
)

def _target_name_with_suffix(target, suffix):
    return target.label.name + suffix

def _google_java_format_aspect_impl(target, aspect_ctx):
    # Skip a target if it doesn't provide a `JavaCompilationInfo`.
    if JavaCompilationInfo not in target:
        return [GoogleJavaFormatAspectInfo()]
    
    # Extract some information from the target and the toolchains:
    srcs = target[JavaCompilationInfo].srcs
    google_java_format_deploy_jar = _extract_google_java_format_deploy_jar(aspect_ctx)
    java_executable = _extract_java_executable(aspect_ctx)

    # Write all of this target's srcs to a file using a `ctx.actions.args()`.
    # TODO(dwtj): Consider re-using a srcs args file output by the target.
    srcs_args = aspect_ctx.actions.args()
    srcs_args.add_all(
        srcs,
        map_each = _to_path,
    )
    srcs_args_file = aspect_ctx.actions.declare_file(_target_name_with_suffix(target, ".java_srcs.args"))
    aspect_ctx.actions.write(
        output = srcs_args_file,
        content = srcs_args,
        is_executable = False,
    )

    # Declare an output log file for `google-java-format` to write to.
    output_file = aspect_ctx.actions.declare_file(_target_name_with_suffix(target, ".google_java_format.log"))

    # Instantiate a script which will run `google-java-format` from a template.
    run_google_java_format = aspect_ctx.actions.declare_file(_target_name_with_suffix(target, ".run_google_java_format.sh"))
    aspect_ctx.actions.expand_template(
        template = aspect_ctx.file._run_google_java_format_template,
        output = run_google_java_format,
        substitutions = {
            "{JAVA_EXECUTABLE}": java_executable.path,
            "{GOOGLE_JAVA_FORMAT_DEPLOY_JAR}": google_java_format_deploy_jar.path,
            "{JAVA_SOURCES_ARGS_FILE}": srcs_args_file.path,
            "{OUTPUT_FILE}": output_file.path,
        },
        is_executable = True,
    )

    # Lastly, run our `google-java-format` script on the target's srcs:
    inputs = depset(
        direct = [
            java_executable,
            google_java_format_deploy_jar,
            srcs_args_file,
        ],
        transitive = [srcs]
    )
    aspect_ctx.actions.run(
        executable = run_google_java_format,
        inputs = inputs,
        outputs = [output_file],
        mnemonic = "GoogleJavaFormat",
        progress_message = "Running `google-java-format` on Java sources of target `" + str(target.label) + "`.",
        use_default_shell_env = False,
    )

    return [
        OutputGroupInfo(default = [output_file]),
        GoogleJavaFormatAspectInfo(output_file = output_file),
    ]

google_java_format_aspect = aspect(
    implementation = _google_java_format_aspect_impl,
    provides = [GoogleJavaFormatAspectInfo],
    attrs = {
        "_run_google_java_format_template": attr.label(
            default = "@dwtj_rules_java//java:aspects/google_java_format_aspect/TEMPLATE.run_google_java_format.sh",
            allow_single_file = True,
        ),
    },
    toolchains = [
        '@dwtj_rules_java//java/toolchains/google_java_format_toolchain:toolchain_type',
        '@dwtj_rules_java//java/toolchains/java_runtime_toolchain:toolchain_type',
    ],
)